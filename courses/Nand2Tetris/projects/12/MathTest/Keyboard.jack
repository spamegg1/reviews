// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {
    static Array keyboard;

    /** Initializes the keyboard.   DONE */
    function void init() {
        let keyboard = 24576;
        return;
    }

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return keyboard[0];
    }

    /**
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character
     * of the pressed key.          DONE */
    function char readChar() {
        var char key;
        while (keyboard[0] = 0) {} // do nothing
        let key = keyboard[0];
        while (~(keyboard[0] = 0)) {} // do nothing, wait for key to be released
        do Output.printChar(key);
        return key;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.     DONE
     */
    function String readLine(String message) {
        var char key;
        var String line;
        do Output.printString(message);  // display message

        let line = String.new(50); // arbitrary line length of 50
        let key = Keyboard.readChar();
        while (~(key = 128)) { // while key pressed is not newline
            if (key = 129) { // if key pressed is backspace
                do line.eraseLastChar();
            }
            else {
                do line.appendChar(key);
            }
            let key = Keyboard.readChar();
        }
        return line;
    }

    /**
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces.     DONE
     */
    function int readInt(String message) {
        var String line;
        let line = Keyboard.readLine(message);
        return line.intValue();
    }
}
